plugins {
    alias(libs.plugins.jetbrains.kotlin.jvm)
    id 'com.gradleup.shadow' version '8.3.6'
}

group = 'xyz.regulad'
version = '1.0-SNAPSHOT'

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    implementation(project(":gplayapi:lib"))
    
    // dependencies below are mutual between gplayapi and cli; 
    // both use the same version of the library so the shadow jar can be used without conflicts
    // so they are included in the library definitions (which are symlinked from the gplayapi submodule)
    implementation(libs.squareup.okhttp)
    implementation(libs.google.gson)
    
    // dependencies below are NOT needed by gplayapi and as such are not included in the library definitions
    implementation("com.github.ajalt.clikt:clikt:5.0.3")
}

test {
    useJUnitPlatform()
}
kotlin {
    jvmToolchain(21)
}

shadowJar {
//    minimize() // breaks clikt -> mordant, which loads classes dynamically based on available resources

    archiveBaseName.set('GPlayApiCli')
    archiveClassifier.set('')
    archiveVersion.set('')
    
    manifest {
        attributes 'Main-Class': 'xyz.regulad.MainKt'
    }
    
    from("gplayapi/lib/src/main/res/raw")
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    // no compression options
}
